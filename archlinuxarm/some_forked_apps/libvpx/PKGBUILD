pkgname=libvpx
pkgver=1.10.0
pkgrel=1
pkgdesc='VP8 and VP9 codec'
arch=(armv7h)
url=http://www.webmproject.org/
license=(BSD)
depends=(gcc-libs)
makedepends=(
  git
  nasm
)
provides=(libvpx.so)
_tag=b41ffb53f1000ab2227c1736d8c1355aa5081c40
source=(git+https://chromium.googlesource.com/webm/libvpx#tag=${_tag}
        0001-ARM-fixes.patch)
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd libvpx

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd libvpx

  patch -p1 -i ../0001-ARM-fixes.patch
}

build() {
  cd libvpx

 # [[ $CARCH != "aarch64" ]] && CONFIG="--disable-neon --disable-neon-asm"
  ./configure \
    --prefix=/usr \
    --disable-install-docs \
    --disable-install-srcs \
    --enable-pic \
    --enable-postproc \
    --enable-runtime-cpu-detect \
    --enable-shared \
    --enable-vp8 \
    --enable-vp9 \
    --enable-vp9-highbitdepth \
    --enable-vp9-temporal-denoising $CONFIG
  make
}

package() {
  cd libvpx

  make DIST_DIR="${pkgdir}"/usr install
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/libvpx/
}

# vim: ts=2 sw=2 et:
